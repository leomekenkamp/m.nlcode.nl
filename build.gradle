plugins {
    id 'application'
//    id 'org.javamodularity.moduleplugin' version '1.8.9'
    //id 'org.openjfx.javafxplugin' version '0.0.14'
    id "org.beryx.jlink" version "2.24.1"
}

defaultTasks 'build'

repositories {
    mavenCentral()
}

apply plugin: 'nl.nlcode.javafxplugin'

javafx {
    version = '20.0.1'
    modules = [ \
        'javafx.controls',
        'javafx.fxml',
    ]
}

application {
    mainClass = 'nl.nlcode.m.JfxLauncher'
    mainModule = 'nl.nlcode.m'
    applicationName = 'm_nlcode_nl'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// higher than 17 yields an error during build on Temurin 20 (or some lower):
// * What went wrong:
// Execution failed for task ':prepareModulesDir'.
// > Unsupported class file major version 62
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = 'nl.nlcode'
version = '1.0.0'

ext {
    jacksonVersion = '2.14.2'
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7';
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.8'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.2'
    implementation group: 'uk.co.xfactory-librarians', name: 'coremidi4j', version: '1.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "$jacksonVersion"

    //testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jlink {
//    addExtraDependencies("javafx")
    options = [
        '--compress', '2', 
        '--no-header-files', 
        '--no-man-pages',
        '--include-locales', 'en,nl'
    ]
    launcher {
        name = "m.nlcode.nl-${version}"
        jvmArgs = ['-Xmx2048M']
    }
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            // install .NET 6 or higher
            // install https://wixtoolset.org/docs/wix3/
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
            icon = "src/main/resources/m.nlcode.nl.ico"
        }
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            icon = "src/main/resources/m.nlcode.nl.icns"
        }
    }
    mergedModule {
        //requires 'java.management';
        requires 'java.naming';
        //requires 'java.logging';
        //requires 'java.sql';
        //requires 'java.xml';
        requires 'java.desktop';
        //requires 'jdk.unsupported';
        requires 'jdk.localedata';
        provides 'javax.sound.midi.spi.MidiDeviceProvider' with 'uk.co.xfactorylibrarians.coremidi4j.CoreMidiDeviceProvider';
    }
}
