plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.runtime' version '1.13.0'
}

defaultTasks 'build'

repositories {
    mavenCentral()
}

javafx {
    modules = [ \
        'javafx.controls',
        'javafx.fxml',
    ]
}

application {
    mainClass = 'nl.nlcode.m.Launcher'
    applicationName = 'm_nlcode_nl'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        noConsole = true
    }
    jpackage {
        // Uncomment and adjust the following line if your runtime task is configured to generate images for multiple platforms
        // targetPlatformName = "mac"

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/m.nlcode.nl.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'ACME']

        if (currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'bla','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'bla']
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

//mainClassName = 'nl.nlcode.m/nl.nlcode.m.ui.App'
//mainClassName = 'nl.nlcode.m.ui.App'

group = 'nl.nlcode'
version = '1.0.0'
sourceCompatibility = '1.16'

ext {
    jacksonVersion = '2.14.2'
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30';
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.1'
    implementation 'uk.co.xfactory-librarians:coremidi4j:1.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "$jacksonVersion"

    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
